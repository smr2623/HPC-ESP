 MODULE FPSM2D_OPERATOR
 
  USE FPSM2D_PINPUT
  USE FPSM2D_DERIV
  
  IMPLICIT NONE
  
  INTEGER, PARAMETER, PRIVATE :: ILAMBDA=FPSM2D_DDATA_VP,&
                                 ITWOMU=FPSM2D_DDATA_VS,&
								 IDT2OVERRHO=FPSM2D_DDATA_RHO
  
  TYPE FPSM2D_OPERATOR_TYPE
   PRIVATE
   REAL, DIMENSION(:,:,:), POINTER :: S
   REAL, DIMENSION(:,:), POINTER :: TMP
   REAL, DIMENSION(:,:,:), POINTER :: MODEL
   TYPE(FPSM2D_DERIV_TYPE), DIMENSION(2) :: D
  END TYPE FPSM2D_OPERATOR_TYPE
  
  CONTAINS
  
!================================================== 

  SUBROUTINE FPSM2D_OPERATOR_NEW(THIS,INPUT,OK)
   TYPE(FPSM2D_OPERATOR_TYPE), INTENT(INOUT) :: THIS
   TYPE(FPSM2D_PINPUT_TYPE), INTENT(IN) :: INPUT
   LOGICAL, INTENT(OUT) :: OK
  !------------------------------
   INTEGER, DIMENSION(2) :: N,ND
   INTEGER :: I,NT,NTRES
   REAL :: DT
   REAL, DIMENSION(2) :: DD
   
   CALL FPSM2D_PINPUT_GETSPACE(INPUT,N,ND)
   CALL FPSM2D_PINPUT_GETDDD(INPUT,DD,DT)
   OK=.TRUE.
   DO I=1,2
    IF(OK) CALL FPSM2D_DERIV_NEW(THIS%D(I),INPUT,I,DD(I),OK)
   ENDDO
   
   IF(OK) THEN
    ALLOCATE(THIS%S(N(1),ND(2),3))
    ALLOCATE(THIS%TMP(N(1),ND(2)))
    ALLOCATE(THIS%MODEL(N(1),ND(2),3))
   
    CALL FPSM2D_PINPUT_GETNT(INPUT,NT,NTRES)
	
    CALL FPSM2D_PINPUT_GETMODEL(INPUT,THIS%MODEL)
	
	THIS%MODEL(:,:,ITWOMU)=THIS%MODEL(:,:,FPSM2D_DDATA_VS)*&
	                       THIS%MODEL(:,:,FPSM2D_DDATA_VS)*&
						   THIS%MODEL(:,:,FPSM2D_DDATA_RHO)*2.0
						   
	THIS%MODEL(:,:,ILAMBDA)=THIS%MODEL(:,:,FPSM2D_DDATA_VP)*&
	                       THIS%MODEL(:,:,FPSM2D_DDATA_VP)*&
						   THIS%MODEL(:,:,FPSM2D_DDATA_RHO)-&
                           THIS%MODEL(:,:,ITWOMU)
				  
    THIS%MODEL(:,:,IDT2OVERRHO)=DT*DT/THIS%MODEL(:,:,FPSM2D_DDATA_RHO)
   
   ELSE
   
    PRINT*, 'ERROR IN FPSM2D_OPERATOR_NEW' 
   ENDIF
  
  END SUBROUTINE FPSM2D_OPERATOR_NEW
!==================================================

 SUBROUTINE FPSM2D_OPERATOR_KILL(THIS)
  TYPE(FPSM2D_OPERATOR_TYPE), INTENT(INOUT) :: THIS
  !------------------------
  INTEGER :: I
  
  DO I=1,2
   CALL FPSM2D_DERIV_KILL(THIS%D(I))
  ENDDO
  DEALLOCATE(THIS%S)
  DEALLOCATE(THIS%TMP)
  DEALLOCATE(THIS%MODEL)
 
 END SUBROUTINE FPSM2D_OPERATOR_KILL
!================================================== 

 SUBROUTINE FPSM2D_OPERATOR_APPLY(THIS,U,DU)
  TYPE(FPSM2D_OPERATOR_TYPE), INTENT(INOUT) :: THIS
  REAL, DIMENSION(:,:,:), INTENT(IN) :: U
  REAL, DIMENSION(:,:,:), INTENT(OUT) :: DU
  !------------------------------------------
  
  CALL FPSM2D_DERIV_APPLY(THIS%D(1),U(:,:,1),THIS%S(:,:,1))
  CALL FPSM2D_DERIV_APPLY(THIS%D(2),U(:,:,1),THIS%S(:,:,3))
  CALL FPSM2D_DERIV_APPLY(THIS%D(1),U(:,:,2),THIS%TMP)
  THIS%S(:,:,3)=THIS%S(:,:,3)+THIS%TMP
  CALL FPSM2D_DERIV_APPLY(THIS%D(2),U(:,:,2),THIS%S(:,:,2))
  
 ! CALL ECHOS(THIS%S,3,'STRAIN')
  !now use TMP as dilatation:
  THIS%TMP=THIS%S(:,:,1)+THIS%S(:,:,2)
  
  !NOW S WILL BECOME STRESS:
  THIS%TMP=THIS%TMP*THIS%MODEL(:,:,ILAMBDA)
  THIS%S(:,:,1)=THIS%TMP+THIS%MODEL(:,:,ITWOMU)*THIS%S(:,:,1)
  THIS%S(:,:,2)=THIS%TMP+THIS%MODEL(:,:,ITWOMU)*THIS%S(:,:,2)
  THIS%S(:,:,3)=0.5*THIS%MODEL(:,:,ITWOMU)*THIS%S(:,:,3)
  
  !CALL ECHOS(THIS%S,3,'STRESS')
  
  !DERIVE STRESS:
  CALL FPSM2D_DERIV_APPLY(THIS%D(1),THIS%S(:,:,1),DU(:,:,1))
  CALL FPSM2D_DERIV_APPLY(THIS%D(2),THIS%S(:,:,3),DU(:,:,2))
  !CALL ECHOS(DU,2,'DSXXXSXYY')
  DU(:,:,1)=THIS%MODEL(:,:,IDT2OVERRHO)*(DU(:,:,1)+DU(:,:,2))
  !CONVERT ALSO FORCE TO DISPLACEMENT INCREMENT
  
  CALL FPSM2D_DERIV_APPLY(THIS%D(2),THIS%S(:,:,2),DU(:,:,2))
  CALL FPSM2D_DERIV_APPLY(THIS%D(1),THIS%S(:,:,3),THIS%TMP)
  DU(:,:,2)=THIS%MODEL(:,:,IDT2OVERRHO)*(DU(:,:,2)+THIS%TMP) 

  
 END SUBROUTINE FPSM2D_OPERATOR_APPLY
 !==================================================
 
 SUBROUTINE ECHOS(S,NC,SLABEL)
  REAL, DIMENSION(:,:,:), INTENT(IN) :: S
  INTEGER, INTENT(IN) :: NC
  CHARACTER(LEN=*), INTENT(IN) :: SLABEL
  !-------
  INTEGER :: ME,IERR,IC,IU
  CHARACTER(LEN=2) :: CN
  
  CALL MPI_COMM_RANK(MPI_COMM_WORLD,ME,IERR)  
  
  WRITE(CN,'I1') ME
  
  DO IC=1,NC
   IU=80+ME
   WRITE(CN,'(I1,I1)') ME,IC
   OPEN(IU,FILE=SLABEL//CN//'.BIN',ACCESS='STREAM',FORM='UNFORMATTED')
   WRITE(IU) S(:,:,IC)
   CLOSE(IU)
  ENDDO

 END SUBROUTINE ECHOS
 !==================================================
 
 END MODULE FPSM2D_OPERATOR