PROGRAM FPSM2D_INPUTGENERATOR
!PURPOSE:
!GENERATE TEST INPUT FILES FOR FPSM2D 
  USE FPSM2D_DDATA
  USE FPSM2D_FILEUNITS
  
  IMPLICIT NONE

  INTEGER, PARAMETER :: NT=500,NTRES=10
  INTEGER, PARAMETER :: NX=256,NY=256
  REAL, PARAMETER :: DX=100.0,DY=100.0,DT=0.01
  REAL, PARAMETER :: VP1=1700.0,VS1=1000.0,RHO1=2000.0
  REAL, PARAMETER :: VP2=3400.0,VS2=2000.0,RHO2=2400.0
  REAL, PARAMETER :: UX=1.0,UY=1.0,RMAX=1000.0
  INTEGER, PARAMETER :: IXS=120,IYS=100 !SOURCE POSITION
  !-------------------
  REAL, DIMENSION(NX,NY,2) :: PLANE
  INTEGER :: I
  
  
  OPEN(FPSM2D_FILEUNITS_GENINPUT,FILE=FPSM2D_FILEUNITS_GENINPUT_FNAME)
   WRITE(FPSM2D_FILEUNITS_GENINPUT,*) NX,NY,NT,NTRES
   WRITE(FPSM2D_FILEUNITS_GENINPUT,*) DX,DY,DT
  CLOSE(FPSM2D_FILEUNITS_GENINPUT)
  
  CALL MODEL(1,VP1,VP2)
  CALL MODEL(2,VS1,VS2)
  CALL MODEL(3,RHO1,RHO2)
  
  DO I=1,2
   CALL UOLD(I)
  ENDDO
  
  STOP
  
  CONTAINS
 !==========================================
 
  SUBROUTINE MODEL(IDD,V1,V2)
   INTEGER, INTENT(IN) :: IDD
   REAL, INTENT(IN) :: V1,V2
   !---------------------------
   INTEGER :: NYH,IY
   LOGICAL :: OK
   
   NYH=NY/2
   
   PLANE(1:NX,1:NYH,1)=V1
   PLANE(1:NX,NYH+1:NY,1)=V2
   
   CALL FPSM2D_DDATA_SAVE(PLANE(:,:,1),IDD,OK)
   IF(.NOT.OK) STOP
  
  END SUBROUTINE MODEL
 !==========================================
 

 SUBROUTINE UOLD(IDD)
  INTEGER, INTENT(IN) :: IDD
  !-----------------------------
  INTEGER :: J,ID1
  LOGICAL :: OK
  REAL :: T0
   
  CALL MAKEFIELD(IDD*DT)
  ID1=FPSM2D_DDATA_OLD+2*(IDD-1)
  DO J=1,2
   CALL FPSM2D_DDATA_SAVE(PLANE(:,:,J),ID1-1+J,OK)
  ENDDO
  
  IF(.NOT.OK) STOP
 
 END SUBROUTINE UOLD
!==========================================
 
  SUBROUTINE MAKEFIELD(DELAY)
   REAL, INTENT(IN) :: DELAY
   INTEGER :: IX,IY
   REAL :: DIX,DIX2,DIY,DIY2,TWOPI,R,A
   REAL :: RFRONT,RMIN,T,T0,PHI
   
   TWOPI=8.0*ATAN(1.0)
   RMIN=DELAY*VP1
   RFRONT=RMAX+RMIN
   T0=RMAX/VP1
   
   DO IY=1,NY
    DIY=(IYS-IY)*DY
	DIY2=DIY*DIY
    DO IX=1,NX
     DIX=(IXS-IX)*DX
	 DIX2=DIX*DIX
	 R=SQRT(DIY2+DIX2)
	 IF(R.LT.RFRONT.AND.R.GT.RMIN) THEN
	  T=(RFRONT-R)/VP1
	  PHI=TWOPI*T/T0
	  A=SIN(PHI)*(1.0-COS(PHI))/SQRT(R)
      PLANE(IX,IY,1)=A*DIX
	  PLANE(IX,IY,2)=A*DIY
	 ELSE
	  PLANE(IX,IY,1:2)=0.0
	 ENDIF
    ENDDO
   ENDDO
   !NOW PLANE IS DISTANCE FROM SOURCE POSITION
   

  END SUBROUTINE MAKEFIELD
!==========================================
END PROGRAM FPSM2D_INPUTGENERATOR